name: "Hearts"
author: "Mike McEuen"
contact: "srmatanza@gmail.com"
possiblePlayers: 4
initialPhase: "lobby"

gvar gPlayerPassed = 0
gvar _gPlayedTrick = 0
gvar gTrickWinner = ""
gvar gLedCard = ""
gvar _gPassRecipient = [1, 3, 2, 0]
gvar gRoundIdx = 0
gvar gMaxPoints = 100
gvar gHeartsBroken = false
gvar _gTrickTaken = false
gvar _gCardsDealt = false
gvar _gDoneScoring = false

pvar pPoints = 0
pvar _pPassCards = true
pvar _pScoreHandCreated = false

phase lobby
  action start_game
    name: "Start Game"
    change_phase("setup")
  given
    #in($playerCount, $possiblePlayers)
    $playerCount == $possiblePlayers
  endaction
endphase

phase setup
  action deal
    name: "Deal"
    deal(52)
    _gCardsDealt = true
    new_rif($table, "currentTrick")
  given
    !_gCardsDealt
  endaction

  action pass_cards
    name: "Pass Cards"
    gPlayerPassed = gPlayerPassed + 1
    _pPassCards = false
    # do this here because it guarantees every player will do it
    if !_pScoreHandCreated then
      new_rif($player.cards, "_score")
      _pScoreHandCreated = true
    endif
    if handContainsCard("2C", $player.cards["hand"]) then
      set_player($currentPlayer)
    endif
    move_cards($player.cards["hand"],
               $otherPlayers[($player.idx+_gPassRecipient[gRoundIdx]) % $playerCount].cards["hand"],
               $selectedCards)
  given
    _gCardsDealt
    $selectedCards.length == 3
    _pPassCards
  endaction

  action lead_round
    name: "Lead Round"
    change_phase("trick_taking")
    gPlayerPassed = 0
    _gPlayedTrick = 1
    gTrickWinner = $player
    gLedCard = $selectedCards[0]
    move_cards($player.cards["hand"], $table["currentTrick"], $selectedCards)
    set_player($player.idx + 1)
  given
    $selectedCards.length == 1
    handContainsCard("2C", $selectedCards)
    gPlayerPassed == $playerCount
  endaction
endphase

phase trick_taking
  action play_card
    name: "Play Card"
    advance_player(1)
    _gPlayedTrick = _gPlayedTrick + 1
    if handContainsSuit("H", $selectedCards) then
      gHeartsBroken = true
    endif
    if $selectedCards[0].suit == gLedCard.suit && isHighCard($selectedCards[0], gLedCard) then
      gTrickWinner = $player
      gLedCard = $selectedCards[0]
    endif
    if _gPlayedTrick == $playerCount then
      change_phase("trick_scoring")
      _gPlayedTrick = 0
      set_player(gTrickWinner.idx)
    endif
    move_cards($player.cards["hand"], $table["currentTrick"], $selectedCards)
  given
    $isYourTurn
    $selectedCards.length == 1
    (gLedCard.suit == $selectedCards[0].suit ||
     !handContainsSuit(gLedCard.suit, $player.cards["hand"]))
  endaction
endphase

phase trick_scoring
  action take_trick
    name: "Take the Trick"
    _gTrickTaken = true
    if $player.cards["hand"].length == 0 then
      change_phase("scoring")
    endif
    move_cards($table["currentTrick"], $player.cards["_score"])
  given
    !_gTrickTaken
    gTrickWinner.playerName == $player.playerName
  endaction

  action lead_trick
    name: "Lead a new Trick"
    change_phase("trick_taking")
    gPlayerPassed = 0
    _gPlayedTrick = 1
    _gTrickTaken = false
    gTrickWinner = $player
    gLedCard = $selectedCards[0]
    advance_player(1)
    if handContainsSuit("H", $selectedCards) then
      gHeartsBroken = true
    endif
    move_cards($player.cards["hand"], $table["currentTrick"], $selectedCards)
  given
    _gTrickTaken
    $selectedCards.length == 1
    gTrickWinner.playerName == $player.playerName
    (gHeartsBroken ||
     !handContainsSuit("H", $selectedCards) ||
     (!handContainsSuit("S", $player.cards["hand"]) &&
      !handContainsSuit("D", $player.cards["hand"]) &&
      !handContainsSuit("C", $player.cards["hand"])
     )
    )
  endaction
endphase

phase scoring
  action score_round
    name: "Score Round"
    _gDoneScoring = true
    pPoints @= reduce($player.cards["_score"],
        accumulator + (
          if current.suit == 2 then 1
          elif cardEq("QS", current) then 13
          else 0
          ), 0)
  given
    !_gDoneScoring
  endaction

  action new_round
    name: "New Round"
    change_phase("setup")
    new_round()
    gHeartsBroken = false
    _gCardsDealt = false
    _gTrickTaken = false
    _gDoneScoring = false
    gRoundIdx = (gRoundIdx + 1) % 4
    _pPassCards @= true
    _pScoreHandCreated @= false
  given
    _gDoneScoring
  endaction
endphase
