name: "Hearts"
author: "Mike McEuen"
contact: "srmatanza@gmail.com"
possiblePlayers: 4
initialPhase: "lobby"

gvar gPlayerPassed = 0
gvar _gPlayedTrick = 0
gvar gTrickWinner = ""
gvar gLedCard = ""
gvar _gPassRecipient = [1, 3, 2, 0]
gvar gRoundIdx = 0
gvar gMaxPoints = 1
gvar _gwc = 0
gvar _gwi = 0
gvar gHeartsBroken = false
gvar _gTrickTaken = false
gvar _gCardsDealt = false
gvar _gDoneScoring = false

pvar pPoints = 0
pvar _pPointsThisRound = 0
pvar _pPassCards = true
pvar _pScoreHandCreated = false
pvar _pNextPlayer = 0

phase lobby
  action start_game
    name: "Start Game"
    change_phase("setup")
  given
    $playerCount == $possiblePlayers
  endaction
endphase

phase setup
  action deal
    name: "Deal"
    deal(52)
    _gCardsDealt = true
    new_rif($table, "currentTrick")
    message("Begin the round by passing three cards to a neighbor!")
  given
    !_gCardsDealt
  endaction

  action pass_cards
    name: "Pass Cards"
    gPlayerPassed = gPlayerPassed + 1
    _pPassCards = false
    _pNextPlayer = ($player.idx+_gPassRecipient[gRoundIdx]) % $playerCount
    set_rif($player.rifs["hand"], null, null, SINGLE)
    if !_pScoreHandCreated then
      new_rif($player.rifs, "_score")
      _pScoreHandCreated = true
    endif
    if rifContainsCard("2C", $player.rifs["hand"]) then
      if rifContainsCard("2C", $selectedCards) then
        set_current_player(_pNextPlayer)
        message("After everyone passes, you will lead the round with the 2C", 0, _pNextPlayer)
        message("", 0, $player.idx)
      else
        set_current_player($player.idx)
        message("After everyone passes, you will lead the round with the 2C", 0, $player.idx)
      endif
    else
      message("", 0, $player.idx)
    endif
    move_cards($player.rifs["hand"],
               $otherPlayers[_pNextPlayer].rifs["hand"],
               $selectedCards)
  given
    _gCardsDealt
    $selectedCards.length == 3
    _pPassCards
  endaction

  action lead_round
    name: "Lead Round"
    change_phase("trick_taking")
    gPlayerPassed = 0
    _gPlayedTrick = 1
    gTrickWinner = $player
    gLedCard = $selectedCards[0]
    move_cards($player.rifs["hand"], $table["currentTrick"], $selectedCards)
    message("Now play a card that matches the suit of the card that led the trick.")
    message("", 0, $player.idx)
    set_current_player($player.idx + 1)
  given
    $selectedCards.length == 1
    rifContainsCard("2C", $selectedCards)
    gPlayerPassed == $playerCount
  endaction
endphase

phase trick_taking
  action play_card
    name: "Play Card"
    advance_player(1)
    _gPlayedTrick = _gPlayedTrick + 1
    if rifContainsSuit("H", $selectedCards) then
      gHeartsBroken = true
    endif
    if $selectedCards[0].suit == gLedCard.suit && cardIsHigher($selectedCards[0], gLedCard) then
      gTrickWinner = $player
      gLedCard = $selectedCards[0]
    endif
    if _gPlayedTrick == $playerCount then
      change_phase("trick_scoring")
      _gPlayedTrick = 0
      set_current_player(gTrickWinner.idx)
      message("You won the trick! Now take it and lead the next hand!", 0, gTrickWinner.idx)
    else
      message("", 0, $player.idx)
    endif
    move_cards($player.rifs["hand"], $table["currentTrick"], $selectedCards)
  given
    $isYourTurn
    $selectedCards.length == 1
    (gLedCard.suit == $selectedCards[0].suit ||
     !rifContainsSuit(gLedCard.suit, $player.rifs["hand"]))
  endaction
endphase

phase trick_scoring
  action take_trick
    name: "Take the Trick"
    _gTrickTaken = true
    if $player.rifs["hand"].length == 0 then
      change_phase("scoring")
    endif
    move_cards($table["currentTrick"], $player.rifs["_score"])
    message("")
  given
    !_gTrickTaken
    gTrickWinner.playerName == $player.playerName
  endaction

  action lead_trick
    name: "Lead a new Trick"
    change_phase("trick_taking")
    gPlayerPassed = 0
    _gPlayedTrick = 1
    _gTrickTaken = false
    gTrickWinner = $player
    gLedCard = $selectedCards[0]
    advance_player(1)
    if rifContainsSuit("H", $selectedCards) then
      gHeartsBroken = true
    endif
    move_cards($player.rifs["hand"], $table["currentTrick"], $selectedCards)
  given
    _gTrickTaken
    $selectedCards.length == 1
    gTrickWinner.playerName == $player.playerName
    (gHeartsBroken ||
     !rifContainsSuit("H", $selectedCards) ||
     (!rifContainsSuit("S", $player.rifs["hand"]) &&
      !rifContainsSuit("D", $player.rifs["hand"]) &&
      !rifContainsSuit("C", $player.rifs["hand"])
     )
    )
  endaction
endphase

phase scoring
  action score_round
    name: "Score Round"
    _gDoneScoring = true
    _pPointsThisRound @= reduce($player.rifs["_score"].cards,
        accumulator + (
          if current.suit == 2 then 1
          elif cardIsEqual("QS", current) then 13
          else 0
          ), 0)
    # normally shooting the moon gives all other players 26pts
    # but for simplicity, we'll just let it be worth -26pts to the current player
    pPoints @= if _pPointsThisRound == 26 then (pPoints - 26)
               else (pPoints + _pPointsThisRound)
    _pPointsThisRound @= 0

    new_round()
    @[set_rif($player.rifs["hand"], null, null, MULTIPLE)]
    gHeartsBroken = false
    _gCardsDealt = false
    _gTrickTaken = false
    _gDoneScoring = false
    gRoundIdx = (gRoundIdx + 1) % 4
    _pPassCards @= true
    _pScoreHandCreated @= false

    @[if pPoints > gMaxPoints then
    _gwc = _gwc + 1
    _gwi = $player.idx
    change_phase("winner")
    endif]

    if _gwc > 1 then
      message("We have multiple winners!")
    else
      message("We have a winner!")
    endif
  given
    !_gDoneScoring
  endaction

  action new_round
    name: "New Round"
    change_phase("setup")
  given
    _gDoneScoring
  endaction
endphase

phase winner
  action again
    name: "Play Again?"
    change_phase("setup")
  endaction
endphase
